buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.3'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

def tomcatHttpPort = 8080

ext {
	javaVersion = '1.8'
	tomcatVersion = '7.0.59'
	springVersion = '4.2.2.RELEASE'
    myBatis = '3.3.0'
    myBatisSpring = '1.2.3'

    hamcrestVersion = '1.3'
    junitVersion = '4.11'
    mockitoVersion = '1.10.8'
    groovyVersion = '2.4.0'
    spockVersion = '1.0-groovy-2.4'

    servletApiVersion = '3.1.0'

}

sourceCompatibility = javaVersion

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

sourceSets {
    integration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationCompile.extendsFrom testCompile
    integrationRuntime.extendsFrom testRuntime
}

dependencies {

    compile "org.springframework:spring-context:${springVersion}"
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile "org.springframework:spring-orm:${springVersion}"
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'

    compile "org.mybatis:mybatis:${myBatis}"
    compile "org.mybatis:mybatis-spring:${myBatisSpring}"

    compile 'com.h2database:h2:1.4.190'

    testCompile "junit:junit:${junitVersion}"
    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "org.mockito:mockito-all:${mockitoVersion}"
    testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'

    providedCompile "javax.servlet:javax.servlet-api:${servletApiVersion}"

	tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"

}

tomcat {
    httpPort = tomcatHttpPort
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }

    module {
        downloadJavadoc = true
        downloadSources = true
        testSourceDirs += file('src/integration/groovy')
        scopes.TEST.plus += [configurations.integrationCompile]
    }
}

task integration(type: Test) {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath
}

task allTests(type: Test, dependsOn: ['integration', 'test']) {
}

